#!/usr/bin/env perl
use strict;
use warnings;
use YAML::XS;
use Digest::MD5;
use MYDan::Agent::Client::Proxy;
use AnyEvent::Handle;
use AnyEvent::Socket;
use Fcntl qw(:flock SEEK_END);
use MYDan::Agent::FileCache;
use MYDan::Util::FastMD5;
use MYDan::Util::Percent;
use Sys::Hostname;

eval
{
    local $/ = undef;
    my %param = %{ YAML::XS::Load( <> ) };

    my @argv = @{$param{argv}};

    my ( $cv, $rlog_pcb ) = ( AE::cv );

    my $rlog = $ENV{MYDan_rlog} if $param{proxyload} && $ENV{MYDan_rlog};
    $rlog = $param{MYDan_rlog} if ! $param{proxyload} && $param{MYDan_rlog};

    if( $rlog )
    {
        my ( $uuid, $server, $port ) = split /:/, $rlog;
        my %argv = ( 
            uuid => $uuid,
            addr => $server || $ENV{TCPREMOTEIP}, 
            port => $port || $ENV{TCPREMOTEPORT},
        );
        
        warn sprintf "proxy info: host:%s port:%s uuid:%s\n", map{ $argv{$_} ||'' }qw( addr port uuid );
        
        my ( $index, %cv, $w, $hdl ) = ( 0 );
        
        tcp_connect $argv{addr}, $argv{port}, sub {
            my ( $fh ) = @_;
            unless( $fh )
            {
                print "$argv{addr}:$argv{port} tcp_connect: $!";
                $cv->send;
                return;
            }
            $hdl = new AnyEvent::Handle(
                fh => $fh,
                rbuf_max => 10240000,
                wbuf_max => 10240000,
                autocork => 1,
                on_error => sub{
                    warn 'tcp error';
                    undef $hdl;
                    $cv->send;
                },
                on_eof => sub{
                    warn 'tcp close';
                    undef $hdl;
                    $cv->send;
                },
            );
            $hdl->push_write( $argv{uuid} . ':' . hostname . ':' );
        },  sub{ return 5; };
    
        $rlog_pcb = sub { $hdl->push_write( shift. "\n" ) if $hdl; };
    }

    if( $param{single} )
    {
        my ( $skip, $cont ) = ( 0, '' );
        tcp_connect $argv[0], $argv[1]->{port}, sub {
            my ( $fh ) = @_  or die "tcp_connect: $!";
            my $hdl; $hdl = new AnyEvent::Handle(
                fh => $fh,
                on_read => sub {
                    my $self = shift;
                    $self->unshift_read (
                        chunk => length $self->{rbuf},
                        sub {
                            if( $skip ) { print $_[1]; }
                            else
                            {
                                $cont .= $_[1];
                                if( $cont =~ s/^\**#\*MYDan_\d+\*#// )
                                {
                                    print $cont;
                                    $skip = 1;
                                }
                            }
                        }
                    );
                },
                on_error => sub{
                    undef $hdl;
                    $cv->send;
                },
                on_eof => sub{
                    undef $hdl;
                    $cv->send;
                }
            );
            if( my $ef = $ENV{MYDanExtractFile} )
            {
                open my $EF, "<$ef" or die "open $ef fail:$!";
                my $size = (stat $ef )[7];
                $hdl->push_write("MYDanExtractFile_::${size}::_MYDanExtractFile");
                my ( $n, $buf );
                while( $n = sysread( $EF, $buf, 102400 ) )
                {
                    $hdl->push_write($buf);
                }
                close $EF;
            }

            $hdl->push_write($argv[1]->{query});
            $hdl->push_shutdown;
        };
        $cv->recv;
    }
    elsif( $param{proxyload} )
    {
        my $percent =  MYDan::Util::Percent->new( undef, undef, $rlog_pcb ) if $rlog_pcb;

        my $filecache = MYDan::Agent::FileCache->new();
        my $path = "$MYDan::PATH/tmp";

        unless( -d $path ){ mkdir $path;chmod 0777, $path; }
        $path .= '/load.data.';
        for my $f ( grep{ -f } glob "$path*" )
        {
            my $t = ( stat $f )[9];
            unlink $f if $t && $t < time - 3600;
        }

        my $temp  = $path. Digest::MD5->new->add( time.'.'.$$.'.'  )->hexdigest;
        open my $TEMP, '+>>', $temp or die "Can't open '$temp': $!";


        my ( $size, $filemd5, $own, $mode, $ok,  %keepalive );
        my ( $skip, $cont ) = ( 0, '' );
        tcp_connect $argv[0], $argv[1]->{port}, sub {
            my ( $fh ) = @_  or die "tcp_connect: $!";
            my $hdl; $hdl = new AnyEvent::Handle(
                fh => $fh,
                on_read => sub {
                    my $self = shift;
                    $self->unshift_read (

                        chunk => length $self->{rbuf},
                        sub {
                            if( $keepalive{save} )
                            {
                                syswrite $TEMP, $_[1];
                                $percent->add( length $_[1] ) if $percent;
                            }
                            else
                            {
                                $keepalive{cont} .= $_[1];
                                $keepalive{cont} =~ s/^\*+//g;

                                if( length $keepalive{cont} > 1024000 )
                                {
                                    undef $hdl;
                                    $cv->send;
                                }

                                if( $keepalive{cont} =~ s/\**#\*MYDan_\d+\*#(\d+):([a-z0-9]+):(\w+):(\d+):// )
                                {
                                    ( $size, $filemd5, $own, $mode ) = ( $1, $2, $3, $4 );

                                    if( $filecache->check( $filemd5 ) )
                                    {
                                        undef $hdl; $cv->send; $ok = $size;
                                    }

                                    $percent->renew( $size )->add( length $keepalive{cont}  ) if $percent;

                                    syswrite $TEMP, delete $keepalive{cont};
                                    $keepalive{save} = 1;
                                }
                            }
                            $percent->print('ProxyLoad.'.$argv[0].' ..') if $percent;

                        }

                    );
                },
                on_error => sub{
                    undef $hdl;
                    $cv->send;
                },
                on_eof => sub{
                    undef $hdl;
                    $cv->send;
                }
            );

            $hdl->push_write($argv[1]->{query});
            $hdl->push_shutdown;
        };
        $cv->recv;

		unless( defined $ok )
		{
			seek $TEMP, -6, SEEK_END;
			sysread $TEMP, my $end, 6;

			unless( $end =~ /^--- 0\n$/  )
			{
				unlink $temp;
				my $err = $keepalive{cont} || '';
				$err =~ s/\**#\*MYDan_\d+\*#//;
				die "status error $err $end\n";
			}
			truncate $TEMP, $size;

			unless( $filemd5 eq MYDan::Util::FastMD5->hexdigest( $temp ) )
			{
				unlink $temp;
				die "md5 nomatch\n";
			}

		}
        close $TEMP;
		eval{ $filecache->save( $temp ); };
		warn "save filecache fail: $@" if $@;

        my $file = $filecache->check( $filemd5 ) || $temp;

        syswrite( \*STDOUT, "MYDanExtractFile_::${file}::_MYDanExtractFile" );
        syswrite \*STDOUT, "$size:$filemd5:$own:$mode:";
    }

    else
    {
        my ( $ppcb, %percent, %pcb );

        $ppcb = sub
        {
            my ( $info, $node ) = @_;
            $node ||= 'unkown';

            unless( $percent{$node} )
            {
                $percent{$node} = MYDan::Util::Percent->new( 
                    $info, "ProxySend.".hostname." ..", sub{ $pcb{$node} = shift } );
            }
            else
            {
                $percent{$node}->add( $info )->print();
                &$rlog_pcb( "$node => $pcb{$node}" );
            }
        } if $rlog_pcb;
    
        my %result = MYDan::Agent::Client::Proxy->new(
            @{$argv[0]}
        )->run( %{$argv[1]}, pcb => $ppcb );
        print YAML::XS::Dump \%result;
    }
    exit 0;
};

warn $@;
print $@;

exit 1;
