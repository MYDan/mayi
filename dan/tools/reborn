#!/usr/bin/env perl
use strict;
use warnings;

use YAML::XS;
use Sys::Hostname;
use MYDan::Util::OptConf;
use MYDan::Util::Reborn;

$| ++;

=head1 SYNOPSIS

 $0 --ipaddr 10.10.10.2 --netmask 10.0.255.255 --gateway 10.10.10.1 --hostname abc.mydan.org --dns 8.8.8.8 --mac 50-7b-9d-06-00-00
 $0 --ipaddr 10.10.10.2 --netmask 10.0.255.255 --gateway 10.10.10.1 --hostname abc.mydan.org --dns 8.8.8.8 --mac 50-7b-9d-06-00-00 --ks=http://mydan.org/ks.cfg

 $0 --verbose
 $0 --url http://xxx.xxx.xxx/ks.list #default in .config

=cut

my $option = MYDan::Util::OptConf->load();
my %o = $option->get( qw( url=s ks=s ipaddr=s netmask=s gateway=s hostname=s dns=s mac=s verbose ) )->dump();

unless( $o{mac} )
{
    my $macf = '/sys/class/net/eth0/address';
    if ( -f $macf )
    {
        my $mac = `cat $macf`;
        chomp $mac;
        $mac =~ s/:/-/g;
        $o{mac} = $mac if $mac =~ /^[a-zA-Z0-9]{2}-[a-zA-Z0-9]{2}-[a-zA-Z0-9]{2}-[a-zA-Z0-9]{2}-[a-zA-Z0-9]{2}-[a-zA-Z0-9]{2}$/;
    }
}

unless( $o{ipaddr} && $o{netmask} && $o{gateway} )
{
    my ( @cfg, %cfg ) = `cat /etc/sysconfig/network 2>/dev/null;cat /etc/sysconfig/network-scripts/ifcfg-eth0 2>/dev/null`;
    chomp @cfg;
    map{ $cfg{$1} = $2 if $_ =~ /^([A-Za-z0-9]+)=(.+)$/ }@cfg;
    for my $name ( qw( ipaddr netmask gateway ) )
    {
        next if $o{$name};
        my $NAME = uc $name;
        $o{$name} = $cfg{$NAME} if $cfg{$NAME};
    }
}

unless( $o{dns} )
{
    my @c = `cat /etc/resolv.conf 2>/dev/null`;
    chomp @c;
    map{ $o{dns} = $1 if /^nameserver\s+(\d+\.\d+\.\d+\.\d+)$/ }@c;
}

$o{hostname} = hostname unless $o{hostname};

print YAML::XS::Dump \%o;
$option->assert( qw( ipaddr netmask gateway hostname dns mac ) );

MYDan::Util::Reborn->new( %o )->do();
